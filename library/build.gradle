apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName project.version
    }

    packagingOptions {
        exclude 'AndroidManifest.xml'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.1.1'
}

checkstyle {
    toolVersion = "6.15"
    configFile = new File(rootDir, 'checkstyle.xml')
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        Publication(MavenPublication) {
            groupId project.group
            artifactId 'controllers-for-android'
            version project.version
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/library-release.aar")
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['Publication']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'controllers-for-android'
        desc = project.description
        websiteUrl = 'https://github.com/alexbirkett/controllers-for-android'
        issueTrackerUrl = 'https://github.com/alexbirkett/controllers-for-android/issues'
        vcsUrl = 'https://github.com/alexbirkett/controllers-for-android.git'
        licenses = ['Apache-2.0']
        labels = ['android', 'controllers', 'single', 'responsible', 'principle']
        publicDownloadNumbers = true
        version {
            name = project.version
            mavenCentralSync {
                sync = false
            }
        }
    }
}